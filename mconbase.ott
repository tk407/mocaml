metavar value_name, x ::=
  {{ isa string }} {{ coq nat }} {{ coq-equality }} {{ hol string }} {{ ocaml int }}
  {{ lex alphanum }} 
%metavar label, lab ::=
%  {{ isa string }} {{ coq nat }} {{ coq-equality }} {{ hol string }} {{ ocaml int }}
%  {{ lex alphanum }} 
indexvar index, i, j, n, m ::=
  {{ isa nat }} {{ coq nat }}  {{ hol num }} {{ ocaml int }}
  {{ lex numeral }}
% the lex specifications above are not accurate - negative numerals, especially, 
% should be supported

grammar


% Type terms
typexpr, t :: TE_ ::=
  | tunit                           ::   :: unit
  | typexpr -> typexpr'                ::   :: arrow
  | typexpr '*' typexpr'               ::   :: prod
  | con typexpr                        ::   :: concurrent
  | typexpr '+' typexpr'               ::   :: sum
  | ( typexpr )                        :: S :: paren {{ ich [[typexpr]] }} {{ ocaml [[typexpr]] }}


redlabel, rl :: RL_ ::=
  | tau                                    ::   :: tau
  | RE expr                                ::   :: labelled



% Terms
expr, e :: E_ ::=
  | value_name                           ::   :: ident
  | unit                             ::   :: unit
  | expr expr'                           ::   :: apply 
  | expr >>= expr'                       ::   :: bind
  | function value_name : typexpr -> expr          ::   :: function (+ bind value_name in expr +)
  | fix e                                ::   :: fix
  | comp e                               ::    :: comp
  | live e                               ::   :: live_expr
  | { e , e' }                           ::   :: pair
  | pair e e'                            ::   :: inpair
  | proj1 e                              ::   :: proj1
  | proj2 e                              ::   :: proj2
  | fork e e'                            ::   :: fork
  | ret e                                ::   :: ret 
  | ( expr )                             :: S :: paren {{ ich [[expr]] }} {{ ocaml [[expr]] }}
  | inl e                                ::   :: taggingleft
  | inr e                                ::   :: taggingright
  | case e1 of inl x1  => e2 | inr x2 => e3  ::   :: case
  | { v / x } e                        :: M :: subst 
    {{ isa subst_expr [[v]] [[x]] [[e]] }}
    {{ ch (subst_expr [[v]] [[x]] [[e]]) }}
    {{ ocaml (subst_expr [[v]] [[x]] [[e]]) }}
  | { ( fix (function x : t -> e) ) / x' } e                      :: M :: fix_subst 
    {{ isa subst_expr (E_fix  (E_function [[x]] [[t]] [[e]]) ) [[x']] [[e]] }}
    {{ ch (subst_expr (E_fix  (E_function [[x]] [[t]] [[e]]) ) [[x']] [[e]]) }}
    {{ ocaml (subst_expr (E_fix  (E_function [[x]] [[t]] [[e]]) ) [[x']] [[e]]) }}



%Values
value, v :: V_ ::=
  | unit                           ::   :: unit
  | function value_name : typexpr -> expr        ::   :: function
  | live e                         ::   :: live_expr
  | inl v                              ::   :: taggedleft
  | inr v                              ::   :: taggedright
  | { v , v' }                         ::   :: valuepair
  | ( v )                             :: S :: paren {{ ich [[v]] }} {{ ocaml [[v]] }}

%Selects
select, s :: S_ ::=
  | First                              ::   :: First {{ tex 1 }}
  | Second                             ::   :: Second {{ tex 2 }}

%Type environment
G {{ tex \Gamma }} :: G_ ::=
  | empty                              ::   :: em
  | G , value_name : typexpr         ::   :: vn

formula :: formula_ ::=
  | judgement                          ::   :: judgement
  | not ( formula )                    ::   :: not  
        {{ isa Not([[formula]]) }}
        {{ coq not([[formula]]) }}
        {{ hol ~([[formula]]) }}
        {{ ocaml TODO }}
  | value_name = value_name'           ::   :: eqv 
        {{ ich [[value_name]]=[[value_name']] }} {{ ocaml TODO }}


%Terminals
terminals :: terminals_ ::=
  |  ->                                ::   :: arrow      {{ tex \rightarrow }}
  |  function                          ::   :: function   {{ tex \lambda }}
  | |-                                 ::   :: turnstile  {{ tex \vdash }}
  | -->                                ::   :: red        {{ tex \longrightarrow }}
  | '{'                                ::   :: leftbrace  {{ tex \{ }}
  | '}'                                ::   :: rightbrace {{ tex \} }}
  | '['                                ::   :: leftsqbrack  {{ tex [ }}
  | ']'                                ::   :: rightsqbrack {{ tex ] }}
  | con                                ::   :: concurrent {{ tex \textbf{con} }}
  | comp                               ::   :: computation {{ tex comp }}
  | exp                                ::   :: expression {{ tex exp }}
  | live                               ::   :: conlive    {{ tex Live }}
  | fix                                ::   :: fix        {{ tex \textbf{fix} }}
  | tau                                ::   :: tau        {{ tex \tau }}
  | >>=                                ::   :: bind {{ tex >>= }}
  | '*'                                ::   :: product_t    {{ tex * }}
  | ','                                ::   :: pair_elem   {{ tex , }}
  | inl                                ::   :: lefttag     {{ tex \textbf{left} }}
  | inr                                ::   :: righttag    {{ tex \textbf{right} }}
  | case                               ::   :: case    {{ tex Case }}
  | of                                 ::   :: of       {{ tex of }}
  | =>                                 ::   :: doublearrow {{ tex \Rightarrow }}
  | '+'                                ::   :: plus {{ tex + }}
  | ':'                                ::   :: funtype {{ tex : }}

%parsing 
%V_fork_curry <= V_constant
  
% Subsumption: values are expressions
subrules
  v <:: expr

substitutions
  single   expr value_name :: subst  

freevars
  expr value_name :: fv

embed

  {{ hol
val _ = Define `
  (remove_duplicates [] = []) /\
  (remove_duplicates (x::xs) = if (MEM x xs) then remove_duplicates xs 
                               else x::(remove_duplicates xs))
`; }}


defns 
Jtype :: '' ::= 

defn 
value_name : typexpr in G  :: :: VTSin :: VTSin_ by

--------------------------------------------------- :: vn1
value_name : typexpr in G, value_name:typexpr

value_name : typexpr in G
not(value_name = value_name')
--------------------------------------------------- :: vn2
value_name : typexpr in G, value_name':typexpr'





% typing rules
defn 
G |- e : t :: :: Get :: Get_ by

% Primitive functions

G |- e : t 
------------------------ :: ret
G |- ret e : con t

G |- e : (con t1) 
G |- e' : (con t2) 
------------------------ :: fork
G |- fork e e' : (con ((t1 * (con t2)) + ((con t1) * t2)))

------------------------ :: unit
G |- unit : tunit

G |- e : t1 
G |- e' : t2 
----------------------------------------:: inpair
G |- pair e e' :  ( t1 * t2 ) 

G |- e : (t1 * t2)
-------------------------------:: proj1
G |- proj1 e : ( t1 * t2 ) -> t1

G |- e : (t1 * t2) 
-------------------------------:: proj2
G |- proj2 e : ( t1 * t2 ) -> t2


x:t in G   
---------------- :: value_name
G |- x:t

G |- e : t1->t2
G |- e' : t1 
---------------- :: apply
G |- e e' : t2

G,x1: t1 |- e : t
--------------------------- I :: lambda
G |- function x1 : t1 -> e : t1->t

G |- e : t
---------------------------  :: live_exp
G |- live e : con t

G |- e : t
---------------------------  :: comp
G |- ( comp e ) : t

G |- e :  t -> t 
------------------------ :: fix
G |- fix e : t

G |- e : con t
G |- e' : t -> con t'
----------------------- :: bind
G |- e >>= e' : con t'

G |- e : t1
G |- e' : t2 
---------------- :: pair
G |- :E_pair: {e,e'} : (t1*t2)


% Sum type things
     G |- e : t
--------------------------- :: TInl
G |- inl e : t + t' 

     G |- e : t
--------------------------- :: TInr
G |- inr e : t' + t 

G |- e : t + t'
G,x : t |- e' : t''
G,x' : t' |- e'' : t''
--------------------------- :: TCase
G |- case e of inl x => e' | inr x' => e'' : t''

%evaluation rules
defns 
Jop :: JO_ ::= 

defn 
e [ s ] --> [ rl ]  e' :: :: red :: red_ {{ tex [[e]]\, \overset{ [[rl]] }{\underset{ [[s]] }{\longrightarrow} }\, [[e']] }} by 

------------------------------- :: app
(function x : t -> e)  v [ s ] --> [ tau ] {v/x}  e

%Concurrent constructs


------------------------------- :: docomp
 comp e [ s ] --> [ RE e ] e 

e [ s ] --> [ rl ] e''
--------------------------- :: forkeval1
 fork e e' [ s ] --> [ rl ] fork e'' e'

e' [ s ] --> [ rl ] e''
--------------------------- :: forkeval2
fork v e' [ s ] --> [ rl ] fork v e''

%Here should be the only path for concurrency

e [ s ] --> [ rl ] e''
{{ ~ (is_value_of_expr [[ e' ]]) }}
--------------------------- :: forkmove1
 fork (live e)  (live e') [ First ] --> [ rl ] fork (live e'') (live e')

e' [ s ] --> [ rl ] e''
{{ ~ (is_value_of_expr [[ e ]]) }}
--------------------------- :: forkmove2
fork (live e)  (live  e') [ Second ] --> [ rl ] fork (live e) (live  e'') 

--------------------------- :: forkdeath1
fork (live v)  (live e) [ First ] --> [ tau ] live ( inl ( ( { v , ( live e ) } ) ) )

--------------------------- :: forkdeath2
fork (live e)  (live v') [ Second ] --> [ tau ] live ( inr ( { (live e) , v' } ) )

% Now for the evaluation steps that have to modified to incorporate the actual OCaml code



--------------------------- :: ret
ret v [ s ] --> [ tau ] ( live v ) 

e [ s ] --> [ rl ] e'
--------------------------- :: evalret
ret e [ s ] --> [ rl ] ret e'

e [ s ] --> [ rl ] e'
--------------------------- :: evalbind
e >>= e'' [ s ] --> [ rl ] e' >>= e''

e [ s ] --> [ rl ] e'
--------------------------- :: movebind
(live e) >>= e'' [ s ] --> [ rl ] (live e') >>= e'' 


--------------------------- :: dobind
(live v) >>= e [ s ] --> [ tau ] e v 

%Context-application
% Evaluate argument first
% then the function
% the rest is taken care of by the app

e' [ s ] --> [ rl ] e''
--------------  :: context_app2
v e' [ s ] --> [ rl ] v e''

e [ s ] --> [ rl ] e''
------------  :: context_app1
e e' [ s ] --> [ rl ] e'' e'


%Fixpoint operator
% This heavily relies on well typed-ness 

e [ s ] --> [ rl ] e'
------------------------------  :: fix_move
( fix e ) [ s ] --> [ rl ] ( fix e' )

----------------------- :: fix_app
( fix (function x : t -> e) )  [ s ] --> [ tau ] { ( fix (function x : t -> e) ) / x } e

% Pairs
% Note this could be defined to be parallel, but I want to constrain concurrency to fork
e [ s ] --> [ rl ] e' 
----------------------- :: pair_1
{ e , e'' } [ s ] --> [ rl ] { e' , e'' }

e [ s ] --> [ rl ] e'
----------------------- :: pair_2
{ v , e } [ s ] --> [ rl ] { v, e' }


------------------------------------------ :: inpair
 pair v  v' [ s ] --> [ tau ] { v , v' }

e [ s ] --> [ rl ] e''
------------------------------------------ :: inpair_eval1
pair e  e' [ s ] --> [ rl ] pair e''  e' 

e' [ s ] --> [ rl ] e''
------------------------------------------ :: inpair_eval2
pair v  e' [ s ] --> [ rl ] pair v  e'' 

-------------------------------------------:: proj1
proj1 { v , v' } [ s ] --> [ tau ] v

-------------------------------------------:: proj2
proj2 { v , v' } [ s ] --> [ tau ] v'

e [ s ] --> [ rl ] e'
-------------------------------------------:: proj1_eval
proj1 e [ s ] --> [ rl ] proj1 e'

e [ s ] --> [ rl ] e'
-------------------------------------------:: proj2_eval
proj2 e [ s ] --> [ rl ] proj2 e'



%Sum types
    e [ s ] --> [ rl ] e'
-------------------------- :: evalinl
inl e [ s ] --> [ rl ] inl e'

    e [ s ] --> [ rl ] e'
-------------------------- :: evalinr
inr e [ s ] --> [ rl ] inr e'

--------------------------------------------------------- :: evalcaseinl
case (inl v) of inl x => e | inr x' => e' [ s ] --> [ tau ] { v / x } e

--------------------------------------------------------- :: evalcaseinr
case (inr v) of inl x => e | inr x' => e' [ s ] --> [ tau ] { v / x' } e'

                                      e [ s ] --> [ rl ] e'
------------------------------------------------------------------------------------ :: evalcase
case e of inl x => e'' | inr x' => e''' [ s ] --> [ rl ] case e' of inl x => e'' | inr x' => e'''
